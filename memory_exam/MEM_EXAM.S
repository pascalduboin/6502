 NLS
*
*#--------------->
* MEM_EXAM
* DUMP MEM PAGE
*
* (<-) (P) (ESC) (->)
*
*
* P. DUBOIN
* 1 NOV 2024
* LISA 2.5
*
*#--------------->
;
;
;** -- ZPAGE
POINT EPZ $00
PAGE EPZ $01
CH EPZ $24
;
** -- I/O
KBD EQU $C000
STROBE EQU $C010
;
;** -- ROM
PRBL2 EQU $F94A
HOME EQU $FC58
RDKEY EQU $FD0C
CROUT EQU $FD8E
PRBYTE EQU $FDDA
COUT EQU $FDED
;
 ORG $800
 OBJ $800
;
GO:
 LDA #$00
 STA POINT ; START WITH
 STA PAGE ; ZPAGE
;--------------------------------
DISPLAY:
 JSR HOME
 JSR HEADER
 JSR CROUT
 LDY #$00
;
NEWLINE:
 LDX #$0C ; TWELVE BYTES AT A TIME
 TYA
 JSR PRBYTE ; PRINT INDEX
 LDA #$A0
 JSR COUT ; SPACE
A1 LDA (POINT),Y
 JSR PRBYTE ; PRINT HEX
 INY
 BEQ FILLIN ; PAGE DONE?
 DEX
 BNE A1 ; TWELVE YET?
;
ADJUST:
 TYA
 SBC #$0C ; RESET Y
 TAY
 LDA #$A0
 JSR COUT ; SPACE
 LDX #$0C ; TWELVE AGAIN
;
ASCII:
 LDA (POINT),Y
 CMP #$40 ; INVERSE?
 BCS B1 ; NO
 ORA #$C0 ; NORMALIZE
B1 CMP #$80 ; FLASHING?
 BCS B2 ; NO
 ORA #$80 ; NORMALIZE
B2 CMP #$A0 ; CONTROL?
 BCS B3 ; NO
 LDA #$AE ; PUT PERIOD
B3 JSR COUT ; SEND IT
 INY
 BEQ GETCMD ; PAGE DONE?
 DEX
 BNE ASCII ; LINE DONE?
 BEQ NEWLINE
;
FILLIN:
 LDX #$10 ; FILL LAST PARTIAL
 JSR PRBL2 ; LINE WITH SPACES
 LDY #$08 ; ADJUST Y
 BNE ADJUST
;--------------------------------
GETCMD:
 JSR CROUT
C1 JSR RDKEY
 CMP #$9B ; ESCAPE?
 BEQ C2
 CMP #$95 ; RIGHT ARROW?
 BEQ C3
 CMP #$88 ; LEFT ARROW?
 BEQ C4
 CMP #$D0 ; "P"?
 BEQ GETPNUM
 BNE C1 ; NONE OF THE ABOVE
C2 RTS
;
C3 INC PAGE
 LDA PAGE
 CMP #$C0 ; MAKE SURE WE JUMP OVER
 BEQ C3 ; THE SOFT SWITCHES!
 JMP DISPLAY
C4 DEC PAGE
 LDA PAGE
 CMP #$C0 ; MAKE SURE WE JUMP OVER
 BEQ C4 ; THE SOFT SWITCHES!
 JMP DISPLAY
;--------------------------------
GETPNUM:
 JSR HEADER
D1 DEC CH ; SO PRBYTE WILL ALWAYS
 DEC CH ; DISPLAY IN SAME PLACE
D2 LDA KBD
 BPL D2
 STA STROBE
 CMP #$8D ; RETURN?
 BEQ D5 ; YES, EXIT
 EOR #$B0
 CMP #$A ; 0-9?
 BCC D3 ; YES
 ADC #$88
 CMP #$FA ; A-F?
 BCC D2 ; NO
;
D3 LDY #$3 ; LOOP 4 TIMES
 ASL ; THROW AWAY HIGH NYBBLE
 ASL
 ASL
 ASL
D4 ASL ; SHIFT INTO
 ROL PAGE ; PAGE NUMBER
 DEY
 BPL D4
 LDA PAGE
 JSR PRBYTE ; DISPLAY PAGE NUMBER
 JMP D1 ; GET NEXT KEYPRESS
D5 JMP DISPLAY
;
;--------------------------------
;
HEADER:
 LDY #$00
E1 LDA STR,Y
 JSR COUT
 INY
 CPY #$05
 BNE E1
 LDA PAGE
 JMP PRBYTE
;
STR:
 ASC "PAGE="
 BRK
;
 END
